 MACRO
&NAME OP &DCB,&IOU,&TO,&RC=8,&BR=,&J=NO
  LCLC &OPT,&JM
.* QUESTA MACRO SERVE PER APRIRE DEI DATASET E FARE IL TEST DI OPEN
.* SE LA OPEN HA SUCCESSO, SI SALTA OLTRE, ALTRIMENTI  SI FINISCE
.* IL PROGRAMMA CON RETURN CODE=8
.* VOLENDO SI PUO (IN CASO DI OPEN NON FATTA) SALTARE A UNA LABEL
.* PARAMETRI
.* &DCB  LABEL DELLA MACRO DCB DEL FILE DA APRIRE
.* &IOU MODO DI APERTURA I=INPUT O=OUTPUT U=UPDAT
.* &TO SE PRESENTE, SI SALTA A QUESTA LABEL IN CASO DI INSUCCESSO
.* &RC = RETURN CODE DA PASSARE IN CHIUSURA = DEFAULT=8
.*     NELL'APERTURA . ALTRIMENTI SI PROSEGUE IN SEQUENZA
.*  IL DCB SI PUO' DARE ANCHE IN REGISTER NOTATION (RX)
 AIF ('&IOU' EQ 'I').INPUT
 AIF ('&IOU' EQ 'O').OUTPUT
 AIF ('&IOU' EQ 'U').UPDAT
 MNOTE 8,'PARAMETRO NON VALIDO  &IOU '
 MEXIT
.INPUT ANOP
&OPT SETC 'INPUT'
  AGO .OPE
.OUTPUT ANOP
&OPT SETC 'OUTPUT'
 AGO .OPE
.UPDAT ANOP
&OPT SETC 'UPDAT'
.OPE ANOP
 AIF  ('&J' EQ 'NO').NOTIPEJ
&JM SETC 'J'
 MNOTE *,'     OPEN TYPE=J'
.NOTIPEJ ANOP
 MNOTE *,'     OPEN (&DCB.,&OPT.) '
&NAME OPEN (&DCB.,&OPT.),TYPE=&JM
  AIF ('&DCB'(1,1) EQ '(' ).REGISTR
 TM &DCB+48,X'10'
.AIFF  ANOP
 AIF (T'&TO EQ 'O').NOBR
 MNOTE *,'     SE LA OPEN NON RIESCE SI SALTA A  &TO '
 BNO &TO
 MEXIT
.REGISTR TM 48&DCB.,X'10'
 AGO .AIFF
.NOBR  AIF ('&BR'  EQ '').NOBR1
 MNOTE *,'     SE LA OPEN NON RIESCE SI SALTA A  &BR '
 BNO &BR
 MEXIT
.NOBR1 BO *+22
 MNOTE *,'     SE LA OPEN NON RIESCE RETURN   RC=&RC.'
 RET &RC
 MEND
