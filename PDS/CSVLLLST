CSVLLLST TITLE 'CSVLLLST   PROGRAM'
         PRINT NOGEN
CSVLLLST SETAD R12,AMODE=31,RMODE=24,SUB=R10
* ADDRESS OF IN CORE TABLE PASSED AS A PARM
* IN HEXADECIMAL, 8 BYTES, FIXED LENGTH
* IN OUTPUT    THE LIST OF THE TABLE
* THE FIRST LINE OF THE TABLE CONTAINS SOME COUNTERS
* THE SECOND AND THIRD LINE CONTAIN THE JOBNAMES
* LINES FROM THE FOURTH ON CONTAINS DATA (COUNTERS)
         PARMEXEC
         OPD PRINT,O,L=133,R=FBA
         C   R3,=F'8'
         BE  MOVETAB
         MVC I+1(40),=CL40'ADDRESS OF TABLE MUST BE LONG 8'
         BIDI I+41(10),PARML
         PUT PRINT,I
         CLOSE (PRINT)
         RET 8
         DC  CL100' ' FILLER
MOVETAB  MVC I+1(12),=CL12'TABLE AT: '
         MVC I+20(8),0(R2)
         PUT PRINT,I
         SR  R4,R4  CLEAN REGISTER
NEXTCIFR TM  0(R2),X'F0' NUMBER
         BO  NUMBER
* (ALPHABE)
         NI  0(R2),X'0F' DROP SIGN
         SR  R5,R5
         IC  R5,0(R2)
         LA  R5,9(R5)
         SLL R4,4  MAKE ROOM
         OR  R4,R5 PUT DATA IN R4
         B   INCREM
NUMBER   DS  0H
         NI  0(R2),X'0F' DROP SIGN
         SR  R5,R5
         IC  R5,0(R2)
         SLL R4,4  MAKE ROOM
         OR  R4,R5 PUT DATA IN R4
INCREM   LA  R2,1(R2)
         BCT R3,NEXTCIFR
         ST  R4,TABADDR
         LTR R4,R4     ADDRESS = 0 ?
         BNE PROCESSA  NO, FINE
         STSTB
         MVC ST(30),=CL30'ADDRESS NON VALID'
         PUT PRINT,STB
         CLOSE (PRINT)
         RET 8
PROCESSA DS  0H
         STSTB
*        EXDI ST(8),TABADDR(4)
*        PUT PRINT,STB
         L   R4,TABADDR
         USING TABFIRST,R4
         LR  R5,R4
         A   R5,TABSTEP
         ST  R5,TAB14
         A   R5,TABSTEP
         ST  R5,TAB58
         EXDI ST2+10(8),00(4,R4)
         EXDI ST2+20(8),04(4,R4)
         EXDI ST2+30(8),08(4,R4)
         EXDI ST2+40(8),12(4,R4)
         EXDI ST2+50(8),16(4,R4)
         EXDI ST2+60(8),20(4,R4)
         EXDI ST2+70(8),24(4,R4)
         EXDI ST2+80(8),28(4,R4)
         L   R5,TABSTART
         EXDI ST2+100(16),0(8,R5)
         PUT PRINT,ST2
         LA  R9,ST+122    END OF LINE
         LTR R5,R5    EMPTY TABLE
         BZ  ENDOFTAB
NEXTLINE DS  0H
         C   R5,TABEND
         BH  ENDOFTAB
         MVC ST(8),0(R5)
         LA  R8,ST+20
         LR  R7,R5
         A   R7,TABSTEP   NEXT RECORD
* FIND TOTAL FIRST
         LA  R6,8(R5)
         SR  R1,R1
ADDNUM   DS  0H
         A   R1,0(R6)
         LA  R6,4(R6)
         CR  R6,R7
         BL  ADDNUM
         ST  R1,TOTMOD
         BIDI ST+9(10),TOTMOD(4)
         LA  R6,8(R5)
NEXTJOBN CR  R6,R7
         BNL EOENTRY
         CLC 0(4,R6),=F'0'
         BE  INCRETAB
         BIDI 0(9,R8),0(4,R6)
         LA  R8,10(R8)
         LR  R10,R6    CURRENT ENTRY
         SR  R10,R5    DISPLACEMENT
         A   R10,TAB14 FIRST HALF JOBNAME
         MVC 0(4,R8),0(R10)
         LA  R8,4(R8)
         LR  R10,R6    CURRENT ENTRY
         SR  R10,R5    DISPLACEMENT
         A   R10,TAB58 SECOND HALF JOBNAME
         MVC 0(4,R8),0(R10)
         LA  R8,4(R8)
INCRETAB LA  R6,4(R6)
         CR  R8,R9
         BL  NEXTJOBN
         PUT PRINT,STB
         STSTB
         LA  R8,ST+20
         B NEXTJOBN
EOENTRY  DS  0H
         CLC RIGABLA,ST
         BE  AR5STEP
         PUT PRINT,STB
         STSTB
AR5STEP  A   R5,TABSTEP
         B   NEXTLINE
ENDOFTAB DS  0H
         CLOSE (PRINT)
         RET  0
RIGABLA  DC   CL132' '
TOTMOD   DC   F'0'  NUMBER LOADS FOR THIS MODULE
TABADDR  DC   F'0'  TABLE ADDRESS (COUNTERS)
TAB14    DC   F'0'  FIRST   4 BYTES JOBNAME
TAB58    DC   F'0'  SECOND  4 BYTES JOBNAME
I        DC   CL133' '
ST2      DC   CL133' '
         STBST
TABFIRST DSECT
         DS    0D
TABSTEP  DS    F     LENGTH OF EACH LINE IN THE TABLE
TABINUSE DS    F     NUMBER OF USED LINES SO FAR
TABMAX   DS    F     MAX NUMBER OF LINES
TABNJOBS DS    F     NUMBER OF JOBS PER EACH LINE
TABHITS  DS    F     TOTAL NUMBER OF HITS
TABSTART DS    F     START OF TABLE (POINTER TO FIRST USED LINE)
TABEND   DS    F     END   OF TABLE (POINTER TO LAST  USED LINE)
         END
