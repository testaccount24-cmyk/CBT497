 MACRO
&NAME OPD &DCB,&IOU,&DD=,&RC=8,&BR=,&L=,&B=,&R=,&M=,&D=PS,&E=NO,&BU=0, *
               &BL=0,                                                  *
               &J=NO,&S=NO,&O=,&EOD=YES,&K=0,&MNOTE=YES,&KO=,&LABEL=
 LCLC &DCBM,&IOUM,&DDM,&LM,&BM,&RM,&MM(3),&DM,&EM,&BUM,&JM,&SM,&EOM
.* DCB + OPEN
.* (DDNAME=SYSPRINT, LABEL DEL DCB=PRINT / SYSIN/IN ETC)
.* PARAMETRI :
.* DCB  LABEL DEL DCB E ULTIME LETTERE DEL DDNAME
.* IOU  I=INPUT,O=OUTPUT,U=UPDAT (MODO DI APERTURA)
.* DD   DDNAME EVENTUALE (SE DIVERSA DAL DEFAULT)
.* L    LRECL       B   BLKSIZE     R   RECFM
.* M    MACRF       D   DSORG       O   OPTCD
.* E    SE NON = NO , GENERA EXIT LIST
.* S    SE NON = NO , GENERA SYNAD
.* J    SE NON = NO , GENERA OPEN TYPE=J
.* BU   BUFNO
.* DEFAULT : A ZERO (= LIBERA PER ESSERE FISSATI DA SCHEDA DD)
.* DDNAME = SYSIN   FB  80
.* DDNAME = SYSPRINT FBA 121
.* DDNAME = SYSPUNCH   FB 80  PM
.* DDNAME = SYSXXXXX  FB
.* PER UPDAT  MACRF=(GL,PL)
.* PER INPUT  MACRF=GM
.* PER OUTPUT MACRF=PM
&BUM  SETC '&BU'
&LM SETC '0'
&DM SETC  '&D'
&BM SETC '0'
&EM SETC '1'
&JM SETC 'NO'
&EOM SETC '0'
&SM SETC '1'
 AIF ('&IOU' NE 'U').IO
.*
&IOUM SETC 'UPDAT'
&MM(1) SETC 'GL'
&MM(2) SETC 'PL'
 AGO .RECFM
.IO AIF ('&IOU' NE 'O').I
&IOUM SETC 'OUTPUT'
&MM(1) SETC 'PM'
 AGO .RECFM
.I AIF ('&IOU' NE 'I').QUELCHE
&IOUM SETC 'INPUT'
&MM(1) SETC 'GM'
  AGO .RECFM
.QUELCHE  ANOP
&IOUM SETC '&IOU'
 AGO .RECFM
.RECFM  AIF ('&DCB' NE 'PRINT').LRECL
&RM SETC 'FBA'
 AGO .LRECL
.LRECL ANOP
  AIF ('&DCB' EQ 'IN' OR '&DCB' EQ 'PUNCH').L80
 AIF ('&DCB' NE 'PRINT').L0
&LM SETC '121'
 AGO .AD
.L0 ANOP
&LM SETC '0'
 AGO .AD
.L80 ANOP
&LM SETC '80'
.AD ANOP
 AIF ('&S' EQ 'NO').NOSYNAD
&SM SETC '&DCB.SYN'
.NOSYNAD AIF ('&E' EQ 'NO').NOEXLST
&EM SETC '&DCB.EXL'
 AIF ('&LABEL' EQ '').NOEXLST
&EM SETC '&LABEL.EXL'
.NOEXLST AIF ('&IOU' EQ 'O').NOEODAD
 AIF ('&EOD' NE 'YES').NOEODAD
&EOM SETC '&DCB.EOD'
.NOEODAD AIF ('&J' EQ  'NO').NOTYPEJ
&JM SETC 'J'
.NOTYPEJ AIF ('&DD' EQ '').DDN
&DDM SETC '&DD.'
 AGO .LRE
.DDN ANOP
&DDM SETC 'SYS&DCB.'
.LRE AIF ('&L' EQ '').OKL
&LM SETC '&L.'
.OKL AIF ('&B' EQ '').OKB
&BM SETC '&B.'
.OKB AIF ('&R' EQ '').OKR
&RM SETC '&R.'
.OKR ANOP
 AIF ('&M(3)'  EQ '').MD2
&MM(3)  SETC '&M(3)'
.MD2 ANOP
 AIF ('&M(2)'  EQ '').MD1
&MM(2)  SETC '&M(2)'
.MD1 ANOP
 AIF ('&M(1)'  EQ '').OKM
&MM(1) SETC '&M(1)'
.OKM AIF ('&BU' EQ '').OKBU
&BUM SETC '&BU.'
.OKBU ANOP
 AIF ('&MNOTE' EQ 'NO').AFTM
 MNOTE *,'&DCB     DCB                   DDNAME= &DDM, '
 MNOTE *,'     DSORG=&D,LRECL=&LM,BLKSIZE=&BM,RECFM=&RM,BUFL=&BL'
 MNOTE *,'     MACRF=(&MM(1),&MM(2),&MM(3)),SYNAD=&SM,EXLST=&EM,'
 MNOTE *,'     BUFNO=&BUM,OPTCD=&O,KEYLEN=&K,EODAD=&EOM'
.AFTM ANOP
&NAME B IHB&SYSNDX.1
&LABEL  DS 0F
&DCB DCB DDNAME=&DDM,DSORG=&D,LRECL=&LM,BLKSIZE=&BM,RECFM=&RM, *********
               BUFL=&BL,                                       *********
               SYNAD=&SM,EXLST=&EM,EODAD=&EOM,BUFNO=&BUM,  *************
               MACRF=(&MM(1),&MM(2),&MM(3)),OPTCD=&O,KEYLEN=&K
IHB&SYSNDX.1 OP &DCB,&IOU,&KO,J=&JM,RC=&RC,BR=&BR
 MEND
