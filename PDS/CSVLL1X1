CSVLL1X1 TITLE 'CSVLL1X1   PROGRAM'
         PRINT NOGEN
CSVLL1X1 SETAD R12,AMODE=31,RMODE=24,SUB=R10
* ADDRESS OF IN CORE TABLE PASSED AS A PARM
* IN HEXADECIMAL, 8 BYTES, FIXED LENGTH
* IN OUTPUT    THE LIST OF THE TABLE
* THE FIRST LINE OF THE TABLE CONTAINS SOME COUNTERS
* THE SECOND AND THIRD LINE CONTAIN THE JOBNAMES
* LINES FROM THE FOURTH ON CONTAINS DATA (COUNTERS)
         PARMEXEC
         OPD PRINT,O,L=80,R=FB
         C   R3,=F'8'
         BE  MOVETAB
         MVC I+1(40),=CL40'ADDRESS OF TABLE MUST BE LONG 8'
         BIDI I+41(10),PARML
         PUT PRINT,I
         CLOSE (PRINT)
         RET 8
         STBST
MOVETAB  SR  R4,R4  CLEAN REGISTER
NEXTCIFR TM  0(R2),X'F0' NUMBER
         BO  NUMBER
* (ALPHABE)
         NI  0(R2),X'0F' DROP SIGN
         SR  R5,R5
         IC  R5,0(R2)
         LA  R5,9(R5)
         SLL R4,4  MAKE ROOM
         OR  R4,R5 PUT DATA IN R4
         B   INCREM
NUMBER   DS  0H
         NI  0(R2),X'0F' DROP SIGN
         SR  R5,R5
         IC  R5,0(R2)
         SLL R4,4  MAKE ROOM
         OR  R4,R5 PUT DATA IN R4
INCREM   LA  R2,1(R2)
         BCT R3,NEXTCIFR
         ST  R4,TABADDR
         LTR R4,R4     ADDRESS = 0 ?
         BNE PROCESSA  NO, OK
         STSTB
         MVC ST(30),=CL30'ADDRESS NON VALID'
         PUT PRINT,ST
         CLOSE (PRINT)
         RET 8
PROCESSA DS  0H
         L   R4,TABADDR
         USING TABFIRST,R4
         LR  R5,R4
         A   R5,TABSTEP
         ST  R5,TAB14
         A   R5,TABSTEP
         ST  R5,TAB58
*
         L   R5,TABSTART
         LTR R5,R5    EMPTY TABLE
         BZ  ENDOFTAB
NEXTLINE DS  0H
         C   R5,TABEND
         BH  ENDOFTAB
         MVC ST(8),0(R5)
         MVC CURRMOD,0(R5)
         LR  R7,R5
         A   R7,TABSTEP   NEXT RECORD
* FIND  TOTAL  FIRST
         LA  R6,8(R5)
         SR  R1,R1
ADDNUM   DS  0H
         A   R1,0(R6)
         LA  R6,4(R6)
         CR  R6,R7
         BL  ADDNUM
         ST  R1,TOTMOD
         BIDI ST+20(10),TOTMOD(4)
         MVC ST+10(8),=CL8'TOTAL   '
         PUT PRINT,ST
         LA  R6,8(R5)
NEXTJOBN CR  R6,R7
         BNL EOENTRY
         CLC 0(4,R6),=F'0'
         BE  INCRETAB
         MVC ST(8),CURRMOD
         BIDI ST+20(10),0(4,R6)
         LR  R10,R6    CURRENT ENTRY
         SR  R10,R5    DISPLACEMENT
         A   R10,TAB14 FIRST PART  JOBNAME
         MVC ST+10(4),0(R10)
         LR  R10,R6    CURRENT ENTRY
         SR  R10,R5    DISPLACEMENT
         A   R10,TAB58 SECOND PART JOBNAME
         MVC ST+14(4),0(R10)
         PUT PRINT,ST
INCRETAB LA  R6,4(R6)
         B NEXTJOBN
EOENTRY  DS  0H
         A   R5,TABSTEP
         B   NEXTLINE
ENDOFTAB DS  0H
         CLOSE (PRINT)
         RET  0
RIGABLA  DC   CL132' '
TOTMOD   DC   F'0'  NUMBER LOADS FOR THIS MODULE
TABADDR  DC   F'0'  TABLE ADDRESS (COUNTERS)
TAB14    DC   F'0'  FIRST   4 BYTES JOBNAME
TAB58    DC   F'0'  SECOND  4 BYTES JOBNAME
I        DC   CL133' '
ST2      DC   CL133' '
CURRMOD  DC   CL8' '
TABFIRST DSECT
         DS    0D
TABSTEP  DS    F     LENGTH OF EACH LINE IN THE TABLE
TABINUSE DS    F     NUMBER OF USED LINES SO FAR
TABMAX   DS    F     MAX NUMBER OF LINES
TABNJOBS DS    F     NUMBER OF JOBS PER EACH LINE
TABHITS  DS    F     TOTAL NUMBER OF HITS
TABSTART DS    F     START OF TABLE (POINTER TO FIRST USED LINE)
TABEND   DS    F     END   OF TABLE (POINTER TO LAST  USED LINE)
         END
