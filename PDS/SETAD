 MACRO
&NAME SETAD &R,&N=Y,&E=Y,&S=S,&SUB=,&RMODE=24,&AMODE=24
.* R = REGISTRI  EVENTUALMENTE SOTTO FORMA DI LISTA
.* N = SE ESPANDERE IL NOME NELLA SAVE
.* E = SE ESPANDERE GLI EQUATE PER AVERE I REGISTRI NELLA XREF
.* S = NOME DELLA SAVE AREA
.* SUB=  REGISTRO DA USARE PER JMPS/BEGS/ENDS SALTI A SUBROUTINE
 COPY SYSGBL
 LCLA &DISPL,&IND,&IND1
 LCLC &CS,&RE(15)
 AIF ('&SUB'  EQ '').IGNORE
&BALREG SETC '&SUB'
 MNOTE *,'     &SUB USATO PER BAL A SUBROUTINES NEL PGM'
.IGNORE ANOP
&CS SETC '&SYSECT'
 AIF ('&CS' NE '').SAVE
 AIF ('&NAME' NE '').USENAME
&CS SETC 'CS&SYSNDX'
 AGO .CS
.USENAME ANOP
&CS SETC '&NAME'
&IND SETA 1
.CS ANOP
&CS CSECT
&CS       RMODE  &RMODE
&CS       AMODE  &AMODE
.SAVE AIF (&IND EQ 1).NONASAV
 AIF ('&N' EQ 'NO').NONAME
&NAME SAVE (14,12),T,&CS..DATE.&SYSDATE..TIME.&SYSTIME
 AGO .R13
.NONASAV AIF ('&N' EQ 'NO').NONA
 SAVE (14,12),T,&CS..DATE.&SYSDATE..TIME.&SYSTIME
 AGO .R13
.NONA SAVE (14,12),T
 AGO .R13
.NONAME ANOP
&NAME SAVE (14,12),T
.R13 USING &CS,15
 AIF (T'&R(1) EQ 'O').DEFTOT
 AIF ('&R(1)' EQ '13' OR '&R(1)' EQ 'R13' OR '&R(1)' EQ 'RD').DEF
.NO13 ST 13,&S+4
 LR &R(1),13
 LA 13,&S
 ST 13,8(&R(1))
&RE(1)  SETC '&R(1)'
 DROP 15
 BALR &RE(1),0
 USING *,&RE(1)
H&SYSNDX EQU *
 CNOP 0,4
 B *+76
&S DC 18F'0'
 AGO .OTHREG
.DEFTOT ANOP
&RE(1) SETC '13'
 AGO .DEFNEXT
.DEF ANOP
&RE(1) SETC '&R(1)'
.DEFNEXT ST &RE(1),&S+4
 LR 14,&RE(1)
 LA &RE(1),&S
 ST &RE(1),8(14)
  DROP 15
 CNOP 0,4
 USING *,&RE(1)
H&SYSNDX EQU *
&S B *+72
 DC 17F'0'
.OTHREG ANOP
&IND SETA 1
.PIUREG ANOP
&IND1 SETA &IND
&IND SETA &IND+1
&RE(&IND) SETC '&R(&IND)'
 AIF ('&RE(&IND)' EQ '').EQUATE
&DISPL SETA &DISPL+4096
 LA &RE(&IND),4095(&RE(&IND1))
 USING H&SYSNDX+&DISPL,&RE(&IND)
 LA &RE(&IND),1(&RE(&IND))
 AGO .PIUREG
.EQUATE ANOP
 AIF ('&E' EQ 'NO').MEND
 SPACE 5
 AIF (&REGS EQ 1).SKIPREG
 YREGS
.SKIPREG ANOP
YES EQU 232  C'Y'  X'E8'
NO  EQU 213  C'N'  X'D5'
BLANK EQU 64 C' '  X'40'
 EJECT
.MEND MEND
